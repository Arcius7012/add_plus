#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выгрузить(Команда) 
	
	ВыгруженныеДанные = ВыгрузитьОбъектыНаСервере();
		
	Режим = РежимДиалогаВыбораФайла.ВыборКаталога; 
	ДиалогОткрытия = Новый ДиалогВыбораФайла(Режим); 
	ДиалогОткрытия.Заголовок = "Выберите каталог для сохранения файла"; 
	
	ДополнительныеПараметры = Новый Структура("ПредопределенныеИмена, Конфигурация", 
									ВыгруженныеДанные.ПредопределенныеИмена, ВыгруженныеДанные.Конфигурация);
	Оповещение = Новый ОписаниеОповещения("ВыборКаталога_Завершение", ЭтотОбъект, ДополнительныеПараметры);
	ДиалогОткрытия.Показать(Оповещение) 

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВыгрузитьОбъектыНаСервере()
	ЗначениеОбъекта = РеквизитФормыВЗначение("Объект");
	Конфигурация = ЗначениеОбъекта.ВыгрузитьКонфигурациюНаСервере(); 
	ПредопределенныеИмена = ЗначениеОбъекта.ВыгрузитьПредопределенныеИменаНаСервере();
	Возврат Новый Структура("Конфигурация, ПредопределенныеИмена", Конфигурация, ПредопределенныеИмена);
КонецФункции

&НаКлиенте
Процедура ВыборКаталога_Завершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Сообщить("Каталог не выбран");
		Возврат;
	КонецЕсли;
		
	ПараметрыФайла = Новый Структура("ПутьКаталога,ИмяФайла,СохраняемыеДанные", Результат[0], "Vendor.json", ДополнительныеПараметры.Конфигурация);
	СохранитьФайл(ПараметрыФайла); 
	
	ПараметрыФайла = Новый Структура("ПутьКаталога,ИмяФайла,СохраняемыеДанные", Результат[0], "Vendor_predefined.json", ДополнительныеПараметры.ПредопределенныеИмена);
	СохранитьФайл(ПараметрыФайла);

КонецПроцедуры

&НаКлиенте
Процедура СохранитьФайл(ПараметрыФайла)
	
	СистемнаяИнформация = Новый СистемнаяИнформация; 
	Если СтрНайти(Строка(нРег(СистемнаяИнформация.ТипПлатформы)), "linux") > 0 Тогда
		ПутьКФайлу = ПараметрыФайла.ПутьКаталога + "/" + ПараметрыФайла.ИмяФайла;			
	Иначе
		ПутьКФайлу = ПараметрыФайла.ПутьКаталога + "\"+ ПараметрыФайла.ИмяФайла;
	КонецЕсли;

	ТекстовыйДок = Новый ТекстовыйДокумент;
	ТекстовыйДок.УстановитьТекст(ПараметрыФайла.СохраняемыеДанные);        
	Попытка
		ТекстовыйДок.Записать(ПутьКФайлу, КодировкаТекста.UTF8,Символы.ПС);
	Исключение
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не удалось записать файл в указанную каталог"+Символы.ПС+
													ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));	
		Возврат;
	КонецПопытки;
	ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон("Файл %1 успешно сохранен", ПараметрыФайла.ИмяФайла));
КонецПроцедуры

#КонецОбласти
