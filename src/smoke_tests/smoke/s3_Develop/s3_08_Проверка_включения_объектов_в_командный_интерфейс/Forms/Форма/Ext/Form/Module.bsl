#Область ОписаниеПеременных

&НаКлиенте
Перем КонтекстЯдра;
&НаКлиенте
Перем Утверждения;
&НаКлиенте
Перем СтроковыеУтилиты;
&НаКлиенте
Перем ПрефиксОбъектов;
&НаКлиенте
Перем ОтборПоПрефиксу;
&НаКлиенте
Перем ВыводитьИсключения;
&НаКлиенте
Перем ИсключенияИзПроверок;
&Наклиенте
Перем Подсистемы;

#КонецОбласти

#Область ИнтерфейсТестирования

&НаКлиенте
Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	
	КонтекстЯдра = КонтекстЯдраПараметр;
	Утверждения = КонтекстЯдра.Плагин("БазовыеУтверждения");
	СтроковыеУтилиты = КонтекстЯдра.Плагин("СтроковыеУтилиты");
	
	ПутьНастройки = ИмяТеста();
	Настройки(КонтекстЯдра, ПутьНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНаборТестов(НаборТестов, КонтекстЯдраПараметр) Экспорт
	
	Инициализация(КонтекстЯдраПараметр);
	
	Если Не ВыполнятьТест(КонтекстЯдра) Тогда
		// add_plus
		НаборТестов.НачатьГруппу("Пропуск теста", Ложь);
		
		ПараметрыТеста = НаборТестов.ПараметрыТеста("Тест пропущен по настройке");
		НаборТестов.Добавить("ПропуститьТест", ПараметрыТеста, "Пропуск теста");
		// add_plus
		
		Возврат;
	КонецЕсли;
		
	Если Подсистемы.КомандныйИнтерфейс.Количество() = 0 Тогда
		// add_plus
		НаборТестов.НачатьГруппу("Пропуск теста", Ложь);
		
		ПараметрыТеста = НаборТестов.ПараметрыТеста("Не найдены подсистемы с командным интерфейсом");
		НаборТестов.Добавить("ПропуститьТест", ПараметрыТеста, "Пропуск теста");
		// add_plus
		
		Возврат;
	КонецЕсли;
	
	ОбъектыМетаданных = ОбъектыМетаданных(ПрефиксОбъектов, ОтборПоПрефиксу);
	ЕстьТесты = Ложь; // add_plus
	
	Для Каждого ОбъектМетаданных Из ОбъектыМетаданных Цикл
		Если Не ВыводитьИсключения Тогда
			МассивТестов = УбратьИсключения(ОбъектМетаданных.Значение);
		Иначе
			МассивТестов = ОбъектМетаданных.Значение;
		КонецЕсли;		
		Если МассивТестов.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		НаборТестов.НачатьГруппу(ОбъектМетаданных.Ключ, Ложь);
		Для Каждого Элемент Из МассивТестов Цикл
			НаборТестов.Добавить(
				"ТестДолжен_ПроверитьЧтоОбъектВключенВПодсистемы", 
				НаборТестов.ПараметрыТеста(Элемент.ПолноеИмя), 
				КонтекстЯдра.СтрШаблон_("%1 [%2]", Элемент.Имя, НСтр("ru = 'Проверка включения объекта в командный интерфейс'")));
				
			ЕстьТесты = Истина; // add_plus
		КонецЦикла;
	КонецЦикла;
	
	// add_plus
	Если Не ЕстьТесты Тогда
		НаборТестов.НачатьГруппу("Пропуск теста", Ложь);
		
		ПараметрыТеста = НаборТестов.ПараметрыТеста("Отсутствуют тестовые данные");
		НаборТестов.Добавить("ПропуститьТест", ПараметрыТеста, "Пропуск теста");	
	КонецЕсли; 
	// add_plus
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСНастройками

&НаКлиенте
Процедура Настройки(КонтекстЯдра, Знач ПутьНастройки)

	Если ЗначениеЗаполнено(Объект.Настройки) Тогда
		Возврат;
	КонецЕсли;
	
	ВыводитьИсключения = Истина;
	ОтборПоПрефиксу = Ложь;
	ПрефиксОбъектов = "";
	СлужебныеПодсистемы = Новый Массив;
	Подсистемы = Подсистемы();
	ИсключенияИзПроверок = Новый Соответствие;
	ПлагинНастроек = КонтекстЯдра.Плагин("Настройки");		
	Объект.Настройки = ПлагинНастроек.ПолучитьНастройку(ПутьНастройки);
	Настройки = Объект.Настройки;
	
	Если Не ЗначениеЗаполнено(Настройки) Тогда
		Объект.Настройки = Новый Структура(ПутьНастройки, Неопределено);
		Возврат;
	КонецЕсли;

	Если Настройки.Свойство("Префикс") Тогда
		ПрефиксОбъектов = ВРег(Настройки.Префикс);
	КонецЕсли;
	
	Если Настройки.Свойство("ОтборПоПрефиксу") Тогда
		ОтборПоПрефиксу = Настройки.ОтборПоПрефиксу;
	КонецЕсли;
		
	Если Настройки.Свойство("ВыводитьИсключения") Тогда
		ВыводитьИсключения = Настройки.ВыводитьИсключения;
	КонецЕсли;
	
	Если Настройки.Свойство("ИсключенияИзпроверок") Тогда
		ИсключенияИзПроверок(Настройки);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ИсключенияИзПроверок(Настройки)

	Для Каждого ИсключенияИзПроверокПоОбъектам Из Настройки.ИсключенияИзпроверок Цикл
		Для Каждого ИсключениеИзПроверок Из ИсключенияИзПроверокПоОбъектам.Значение Цикл
			ИсключенияИзПроверок.Вставить(ВРег(ИсключенияИзПроверокПоОбъектам.Ключ + "." + ИсключениеИзПроверок), Истина); 	
		КонецЦикла;
	КонецЦикла;	

КонецПроцедуры

#КонецОбласти

#Область Тесты

&НаКлиенте
Процедура ТестДолжен_ПроверитьЧтоОбъектВключенВПодсистемы(ПолноеИмяМетаданных) Экспорт
	
	ПропускатьТест = ПропускатьТест(ПолноеИмяМетаданных);
	Результат = ПроверитьЧтоОбъектВключенВПодсистемыСервер(ПолноеИмяМетаданных, Подсистемы);
	
	Если Не Результат И ПропускатьТест.Пропустить Тогда
		Утверждения.ПропуститьТест(ТекстСообщения(ПолноеИмяМетаданных));
	Иначе
		Утверждения.Проверить(Результат = Истина, ТекстСообщения(ПолноеИмяМетаданных));
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьЧтоОбъектВключенВПодсистемыСервер(ПолноеИмяМетаданных, Подсистемы)
	
	// add_plus
	Если СтрНайти(ПолноеИмяМетаданных, "ПрисоединенныеФайлы") <> 0 Тогда
		Возврат Истина;
	КонецЕсли;
	// add_plus
	
	ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ПолноеИмяМетаданных);
	Результат = Ложь;
	
	Для Каждого ИмяПодсистемы Из Подсистемы.КомандныйИнтерфейс Цикл
		Подсистема = Метаданные.НайтиПоПолномуИмени(ИмяПодсистемы);
		Если ПодсистемаСодержитОбъект(Подсистема, ОбъектМетаданных) Тогда
			Результат = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	// add_plus Уже есть проверка служебной подсистемы
	//Для Каждого ИмяПодсистемы Из Подсистемы.Служебные Цикл
	//	Подсистема = Метаданные.НайтиПоПолномуИмени(ИмяПодсистемы);
	//	Если ПодсистемаСодержитОбъект(Подсистема, ОбъектМетаданных) Тогда
	//		Результат = Истина;
	//		Прервать;
	//	КонецЕсли;
	//КонецЦикла;
	// add_plus
	
	Возврат Результат;

КонецФункции
 
// add_plus
&НаКлиенте
Процедура ПропуститьТест(Знач ПричинаПропускаТеста) Экспорт
	КонтекстЯдра.ПропуститьТест(ПричинаПропускаТеста);
КонецПроцедуры // add_plus

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПропускатьТест(ПолноеИмяМетаданных)

	Результат = Новый Структура;
	Результат.Вставить("ТекстСообщения", "");
	Результат.Вставить("Пропустить", Ложь);
	
	Если ИсключенИзПроверок(ПолноеИмяМетаданных) Тогда
		ШаблонСообшения = НСтр("ru = 'Объект ""%1"" исключен из проверки'");
		Результат.ТекстСообщения = СтроковыеУтилиты.ПодставитьПараметрыВСтроку(ШаблонСообшения, ПолноеИмяМетаданных);
		Результат.Пропустить = Истина;
		Возврат Результат;
	КонецЕсли;
			
	Возврат Результат;

КонецФункции 

&НаКлиенте
Функция ИсключенИзПроверок(ПолноеИмяМетаданных)
	
	Результат = Ложь;
	МассивСтрокИмени = СтроковыеУтилиты.РазложитьСтрокуВМассивПодстрок(ПолноеИмяМетаданных, ".");
	ИслючениеВсехОбъектов = СтроковыеУтилиты.ПодставитьПараметрыВСтроку("%1.*", МассивСтрокИмени[0]);
	
	Если ИсключенияИзПроверок.Получить(ВРег(ПолноеИмяМетаданных)) <> Неопределено
	 Или ИсключенияИзПроверок.Получить(ВРег(ИслючениеВсехОбъектов)) <> Неопределено Тогда
		Результат = Истина;	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция УбратьИсключения(МассивТестов)

	Результат = Новый Массив;
	
	Для Каждого Тест Из МассивТестов Цикл
		Если Не ИсключенИзПроверок(Тест.ПолноеИмя) Тогда
			Результат.Добавить(Тест);
		КонецЕсли;	
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Функция ТекстСообщения(ПолноеИмяМетаданных)

	ШаблонСообщения = НСтр("ru = 'Объект ""%1"" не входит в подсистемы командного интерфейса:
							|%2'"); // add_plus
	Разделитель = СтроковыеУтилиты.ПодставитьПараметрыВСтроку(",%1", Символы.ПС);
	ТекстПодсистемы = СтроковыеУтилиты.СтрокаИзМассиваПодстрок(Подсистемы.КомандныйИнтерфейс, Разделитель);
	ТекстСообщения = СтроковыеУтилиты.ПодставитьПараметрыВСтроку(ШаблонСообщения, ПолноеИмяМетаданных, ТекстПодсистемы);
	
	Возврат ТекстСообщения;

КонецФункции

&НаСервереБезКонтекста
Функция Подсистемы()

	Подсистемы = Новый Структура;
	Подсистемы.Вставить("КомандныйИнтерфейс", Новый Массив);
	Подсистемы.Вставить("Служебные", Новый Массив);
	
	Для Каждого Подсистема Из Метаданные.Подсистемы Цикл
		Если Подсистема.ВключатьВКомандныйИнтерфейс Тогда
			Подсистемы.КомандныйИнтерфейс.Добавить(Подсистема.ПолноеИмя());
		Иначе
			Подсистемы.Служебные.Добавить(Подсистема.ПолноеИмя());
		КонецЕсли;
	КонецЦикла;

	Возврат Подсистемы;
		
КонецФункции

&НаСервереБезКонтекста
Функция ОбъектыМетаданных(ПрефиксОбъектов, ОтборПоПрефиксу)
	
	// add_plus Ограничиваем выборку
	ОбъектыМетаданных = Новый Структура;
	//ОбъектыМетаданных.Вставить("ОбщиеМодули", Новый Массив);
	//ОбъектыМетаданных.Вставить("ПараметрыСеанса", Новый Массив);
	//ОбъектыМетаданных.Вставить("Роли", Новый Массив);
	//ОбъектыМетаданных.Вставить("ПланыОбмена", Новый Массив);
	//ОбъектыМетаданных.Вставить("КритерииОтбора", Новый Массив);
	//ОбъектыМетаданных.Вставить("ПодпискиНаСобытия", Новый Массив);
	//ОбъектыМетаданных.Вставить("РегламентныеЗадания", Новый Массив);
	//ОбъектыМетаданных.Вставить("ФункциональныеОпции", Новый Массив);
	//ОбъектыМетаданных.Вставить("ПараметрыФункциональныхОпций", Новый Массив);
	//ОбъектыМетаданных.Вставить("ОпределяемыеТипы", Новый Массив);
	//ОбъектыМетаданных.Вставить("ХранилищаНастроек", Новый Массив);
	//ОбъектыМетаданных.Вставить("ОбщиеФормы", Новый Массив);
	//ОбъектыМетаданных.Вставить("ОбщиеКоманды", Новый Массив);
	//ОбъектыМетаданных.Вставить("ГруппыКоманд", Новый Массив);
	//ОбъектыМетаданных.Вставить("ОбщиеМакеты", Новый Массив);
	//ОбъектыМетаданных.Вставить("ОбщиеКартинки", Новый Массив);
	//ОбъектыМетаданных.Вставить("ПакетыXDTO", Новый Массив);
	//ОбъектыМетаданных.Вставить("WebСервисы", Новый Массив); 
	//ОбъектыМетаданных.Вставить("HTTPСервисы", Новый Массив);
	//ОбъектыМетаданных.Вставить("ЭлементыСтиля", Новый Массив);
	//ОбъектыМетаданных.Вставить("Стили", Новый Массив);
	//ОбъектыМетаданных.Вставить("Константы", Новый Массив);
	ОбъектыМетаданных.Вставить("Справочники", Новый Массив);
	ОбъектыМетаданных.Вставить("Документы", Новый Массив);
	ОбъектыМетаданных.Вставить("ЖурналыДокументов", Новый Массив);
	//ОбъектыМетаданных.Вставить("Перечисления", Новый Массив);
	//ОбъектыМетаданных.Вставить("Отчеты", Новый Массив);
	ОбъектыМетаданных.Вставить("Обработки", Новый Массив);
	//ОбъектыМетаданных.Вставить("ПланыВидовХарактеристик", Новый Массив);
	//ОбъектыМетаданных.Вставить("ПланыСчетов", Новый Массив);
	//ОбъектыМетаданных.Вставить("ПланыВидовРасчета", Новый Массив);
	//ОбъектыМетаданных.Вставить("РегистрыСведений", Новый Массив);
	//ОбъектыМетаданных.Вставить("РегистрыНакопления", Новый Массив);
	//ОбъектыМетаданных.Вставить("РегистрыБухгалтерии", Новый Массив);
	//ОбъектыМетаданных.Вставить("РегистрыРасчета", Новый Массив);
	//ОбъектыМетаданных.Вставить("БизнесПроцессы", Новый Массив);
	//ОбъектыМетаданных.Вставить("Задачи", Новый Массив);
	//ОбъектыМетаданных.Вставить("ВнешниеИсточникиДанных", Новый Массив);
	// add_plus
	
	Для Каждого Элемент Из ОбъектыМетаданных Цикл
		Для Каждого ОбъектМетаданных Из Метаданные[Элемент.Ключ] Цикл
			Если ОтборПоПрефиксу И Не ИмяСодержитПрефикс(ОбъектМетаданных.Имя, ПрефиксОбъектов) Тогда
				Продолжить;
			КонецЕсли;
			Если Метаданные.ВнешниеИсточникиДанных.Содержит(ОбъектМетаданных) Тогда
				ОбъектыВнешнегоИсточникаДанных(ОбъектМетаданных, ОбъектыМетаданных[Элемент.Ключ]);
			Иначе	
				СтруктураЭлемента = Новый Структура;
				СтруктураЭлемента.Вставить("Имя", ОбъектМетаданных.ПолноеИмя());
				СтруктураЭлемента.Вставить("ПолноеИмя", ОбъектМетаданных.ПолноеИмя());
				ОбъектыМетаданных[Элемент.Ключ].Добавить(СтруктураЭлемента);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ОбъектыМетаданных;

КонецФункции

&НаСервереБезКонтекста
Процедура ОбъектыВнешнегоИсточникаДанных(ОбъектМетаданных, КоллекцияОбъектовМетаданных)

	КоллекцииВнешнегоИсчтоникаДанных = Новый Структура;
	КоллекцииВнешнегоИсчтоникаДанных.Вставить("Таблицы", "Таблица");
	КоллекцииВнешнегоИсчтоникаДанных.Вставить("Кубы", "Куб");
	
	СтроковыеУтилиты = СтроковыеУтилиты();
	
	Для Каждого КоллекцияВнешнегоИсчтоникаДанных Из КоллекцииВнешнегоИсчтоникаДанных Цикл
		Для Каждого ОбъектВнешнегоИсчтоникаДанных Из ОбъектМетаданных[КоллекцияВнешнегоИсчтоникаДанных.Ключ] Цикл
			
			СтруктураЭлемента = Новый Структура;
			СтруктураЭлемента.Вставить("Имя", ОбъектВнешнегоИсчтоникаДанных.ПолноеИмя());
			СтруктураЭлемента.Вставить("ПолноеИмя", ОбъектВнешнегоИсчтоникаДанных.ПолноеИмя());
			КоллекцияОбъектовМетаданных.Добавить(СтруктураЭлемента);
			
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПодсистемаСодержитОбъект(ПодсистемаРодитель, ОбъектМетаданных)	
	
	Если ПодсистемаРодитель.Состав.Содержит(ОбъектМетаданных) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Для Каждого Подсистема Из ПодсистемаРодитель.Подсистемы Цикл
		Если ПодсистемаСодержитОбъект(Подсистема, ОбъектМетаданных) Тогда
			Возврат Истина;
		КонецЕсли;	
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяСодержитПрефикс(Имя, Префикс)
	
	Если Не ЗначениеЗаполнено(Префикс) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// add_plus
	Возврат СтрНачинаетсяС(ВРег(Имя), ВРег(Префикс));   
	// add_plus
	
КонецФункции

&НаСервереБезКонтекста
Функция СтроковыеУтилиты()
	Возврат ВнешниеОбработки.Создать("СтроковыеУтилиты");	
КонецФункции 

&НаКлиенте
Функция ИмяТеста()
	
	Если Не ЗначениеЗаполнено(Объект.ИмяТеста) Тогда
		Объект.ИмяТеста = ИмяТестаНаСервере();
	КонецЕсли;
	
	Возврат Объект.ИмяТеста;
	
КонецФункции

&НаСервере
Функция ИмяТестаНаСервере()
	Возврат РеквизитФормыВЗначение("Объект").Метаданные().Имя;
КонецФункции

&НаКлиенте
Функция ВыполнятьТест(КонтекстЯдра)
	
	ВыполнятьТест = Ложь;
	ПутьНастройки = ИмяТеста();
	Настройки(КонтекстЯдра, ПутьНастройки);
	Настройки = Объект.Настройки;
	
	Если Не ЗначениеЗаполнено(Настройки) Тогда
		Возврат ВыполнятьТест;
	КонецЕсли;
		
	Если ТипЗнч(Настройки) = Тип("Структура") И Настройки.Свойство("Используется") Тогда
		ВыполнятьТест = Настройки.Используется;	
	КонецЕсли;
	
	Возврат ВыполнятьТест;

КонецФункции

#КонецОбласти